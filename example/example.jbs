#!/usr/bin/env jabs
#This is a comment
set ion 4He
set energy 2027keV
#the following three lines are equivalent
set alpha 0
set alpha 0deg
set alpha "0 deg"
set fluence 2.0e12
load exp "experimental.dat"
#sample can be set with one command
set sample Au 30tfu rough 20tfu n_rough 5 SiO2 6500tfu Si 10000tfu 
#or loaded from a file (tabular data)
#load sample "sample.txt"
set det slope 1.72keV
set det offset 10keV
set det resolution 10keV
set det theta 160deg
set det phi 90deg
set det solid 70msr
add fit_range 200 700
add fit_range 1050 1100
set fit_maxiter 100
#simulate
fit calib,fluence,thickness1,thickness2,rough1
save spectra "out.csv"
save det "det_out.txt"
#fitted sample can be saved (and loaded back in later!)
save sample "sample_out.txt"
